buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "gradle.plugin.org.jlleitschuh.gradle:ktlint-gradle:4.1.0"
    }
}

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.2.61'
    id "org.jetbrains.kotlin.kapt" version "1.2.61"
    id 'maven-publish'
    id "org.jlleitschuh.gradle.ktlint" version "6.1.0"
    id "com.jfrog.bintray" version "1.8.3"
}

ext {
    bintrayRepo = 'maven'
    bintrayName = 'eos-http-rpc'
    bintrayOrg = 'memtrip'

    publishedGroupId = 'com.memtrip.eos-jvm'
    artifact = 'eos-http-rpc'

    libraryDescription = 'An EOS client library that uses an OkHttpClient to make requests to the nodeos RPC HTTP API.'
    siteUrl = 'https://github.com/memtrip/eos-jvm'
    gitUrl = 'https://github.com/memtrip/eos-jvm.git'

    libraryVersion = '1.0.0-alpha23'

    developerId = 'samkirton'
    developerName = 'Samuel Kirton'
    developerEmail = 'sam@memtrip.com'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}

version = libraryVersion
group = publishedGroupId

sourceCompatibility = 1.8

repositories {
    jcenter()
}

dependencies {

    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.2.61"

    /* retrofit */
    compile 'com.squareup.retrofit2:retrofit:2.4.0'
    compile 'com.squareup.retrofit2:converter-moshi:2.4.0'
    compile 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'

    compile 'javax.xml.bind:jaxb-api:2.2.12-b140109.1041'

    /* moshi */
    compile 'com.squareup.moshi:moshi-kotlin:1.6.0'
    kapt 'com.squareup.moshi:moshi-kotlin-codegen:1.6.0'

    /* rxkotlin */
    compile 'io.reactivex:rxkotlin:1.0.0'

    compile ("com.memtrip.eos-jvm:eos-core:1.0.0-alpha14") {
        exclude group: "com.lambdaworks", module: "scrypt"
    }

    /* junit */
    testCompile 'junit:junit:4.12'
    testCompile 'org.junit.platform:junit-platform-runner:1.0.1'

    /* spek */
    testCompile 'org.jetbrains.spek:spek-api:1.1.5'
    testCompile 'org.jetbrains.spek:spek-junit-platform-engine:1.1.5'

    testCompile 'com.squareup.okhttp3:logging-interceptor:3.10.0'
    testCompile 'org.json:json:20140107'

    /* abi */
    testCompile "com.memtrip.eos-jvm:eos-abi-writer:1.0.0-alpha23"
    kaptTest "com.memtrip.eos-jvm:eos-abi-writer-preprocessor:1.0.0-alpha23"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

allprojects {
    task sourcesJar(type: Jar, dependsOn: project.classes) {
        from sourceSets.main.allSource
    }
}

artifacts {
    archives sourcesJar
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')

    configurations = ['archives']
    publications = ['maven']
    pkg {
        version {
            name = libraryVersion
            desc = libraryDescription
            released = new Date()
        }
        repo = bintrayRepo
        name = bintrayName
        userOrg = bintrayOrg
        desc = libraryDescription
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = allLicenses
        publish = true
        publicDownloadNumbers = true
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = publishedGroupId
            artifactId = artifact
            description = libraryDescription
            version = libraryVersion
            pom {
                name = artifact
                description = libraryDescription
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'samkirton'
                        name = 'Samuel Kirton'
                        email = 'sam@memtrip.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/memtrip/eos-jvm.git'
                    developerConnection = 'scm:git:ssh://github.com/memtrip/eos-jvm.git'
                    url = 'https://github.com/memtrip/eos-jvm/'
                }
            }
            artifact sourcesJar {
                classifier = 'sources'
            }
            from components.java
        }
    }
}