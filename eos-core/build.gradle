buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'gradle.plugin.org.jlleitschuh.gradle:ktlint-gradle:6.1.0'
    }
}

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.2.61'
    id 'maven-publish'
    id "org.jlleitschuh.gradle.ktlint" version "6.1.0"
    id "com.jfrog.bintray" version "1.8.3"
}

ext {
    bintrayRepo = 'maven'
    bintrayName = 'eos-core'
    bintrayOrg = 'memtrip'

    publishedGroupId = 'com.memtrip.eos-jvm'
    artifact = 'eos-core'

    libraryDescription = 'An EOS client library containing the core building blocks required to interact with the EOS network.'
    siteUrl = 'https://github.com/memtrip/eos-jvm'
    gitUrl = 'https://github.com/memtrip/eos-jvm.git'

    libraryVersion = '1.0.0-beta03'

    developerId = 'samkirton'
    developerName = 'Samuel Kirton'
    developerEmail = 'sam@memtrip.com'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}

version = libraryVersion
group = publishedGroupId

sourceCompatibility = 1.8

repositories {
    jcenter()
}

jar {
    enabled = true
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.8'
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compile ("org.bitcoinj:bitcoinj-core:0.14.7") {
        exclude group: "com.lambdaworks", module: "scrypt"
    }
    testCompile "junit:junit:4.12"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

allprojects {
    task sourcesJar(type: Jar, dependsOn: project.classes) {
        from sourceSets.main.allSource
    }
}

artifacts {
    archives sourcesJar
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')

    configurations = ['archives']
    publications = ['maven']
    pkg {
        version {
            name = libraryVersion
            desc = libraryDescription
            released = new Date()
        }
        repo = bintrayRepo
        name = bintrayName
        userOrg = bintrayOrg
        desc = libraryDescription
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = allLicenses
        publish = true
        publicDownloadNumbers = true
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = publishedGroupId
            artifactId = artifact
            description libraryDescription
            version = libraryVersion
            pom {
                name = artifact
                description = libraryDescription
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'samkirton'
                        name = 'Samuel Kirton'
                        email = 'sam@memtrip.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/memtrip/eos-jvm.git'
                    developerConnection = 'scm:git:ssh://github.com/memtrip/eos-jvm.git'
                    url = 'https://github.com/memtrip/eos-jvm/'
                }
            }
            artifact sourcesJar {
                classifier = 'sources'
            }
            from components.java
        }
    }
}